"============================================================================
"
" OpenVPN configuration syntax file
"
" Language:	   OpenVPN Configuration File
" Version:     1.01
" Date:        04-Dec-2005
" Maintainer:  Eric Haarbauer <ehaar{DOT}com{AT}grithix{DOT}dyndns{DOT}org>
" License:     This file is placed in the public domain.
"
"============================================================================
" Section:  Notes  {{{1
"============================================================================
"
" This vim syntax script highlights configuration files used with James
" Yonan's OpenVPN application (http://openvpn.net).
" 
" Features:
"
"   * Highlights options, comment lines, and arguments
"   * Recognizes quoted strings and escaped characters (e.g. \\, \")
"   * Flags arguments given for options that take no arguments
"
" Known Issues:
"
"   * Rare combinations of escaped characters and quotes result in incorrect
"     coloration.
"
" Installation:
"
"   Put this file in your $VIMRUNTIME/syntax directory.  You can determine the
"   value of $VIMRUNTIME by entering ":echo $VIMRUNTIME" from within vim.
"
"   Setting up automatic filetype definition requires some knowledge about
"   your system.  The OpenVPN application itself does not require a naming
"   standard for configuration files, although most distribution packages use
"   a .conf suffix.  However, the .conf suffix is used by many other
"   applications with different syntaxes.
"
"   One way to overcome this ambiguity is to incorporate the directory name
"   into the recognition process.  For example, adding the following line to
"   $VIMRUNTIME/filetype.vim should work for many *NIX systems:
"      
"       au BufNewFile,BufRead *openvpn*/*.conf setfiletype openvpn
"
"   On Fedora Core, this technique recognizes configuration files in
"   /etc/openvpn and /usr/share/doc/openvpn-X.X.X/sample-config-files.
"      
"   If filename- and directory-based recognition is impractical, the easiest
"   approach is to embed the following autocommand in your configuration files:
"
"       # vim:ft=openvpn:
"
" Design Notes:
"
"   Part of this script is autogenerated from the output of openvpn --help.
"   The source code for this script is available from the author on request
"   (see email address at the top of the file).  It should build on most Linux
"   systems with openvpn installed.
"
"   The build system that generates this script strips special CVS tokens
"   (like "Id:") so that CVS no longer recognizes them.  This allows users to
"   place this script in their own version control system without losing
"   information.  The author encourages other vim script developers to adopt a
"   similar approach in their own scripts.
"
" Source File: Id: openvpn.src.vim,v 1.7 2005/12/04 17:29:51 ehaar Exp 
"============================================================================
" Section:  Initialization  {{{1
"============================================================================

" For version 5.x: Clear all syntax items
" For version 6.x: Quit when a syntax file was already loaded
if !exists("main_syntax")
  if version < 600
    syntax clear
  elseif exists("b:current_syntax")
    finish
  endif
  let main_syntax = 'openvpn'
endif

" Don't use standard HiLink, it will not work with included syntax files
if version < 508
  command! -nargs=+ OpenvpnHiLink highlight link <args>
else
  command! -nargs=+ OpenvpnHiLink highlight default link <args>
endif

syntax case match

"============================================================================
" Section:  Group Definitions  {{{1
"============================================================================

" Note that use of the \@<! atom may slow processing.
syntax region  openvpnQuote start=+\(\\\)\@<!"+ end=+\(\\\)\@<!"+ contained contains=openvpnSpecial

syntax match   openvpnSpecial       +\\\("\|\\\| \)+ contained
syntax match   openvpnSpuriousValue "\S\+"           contained
syntax match   openvpnComment       "^\(;\|#\).*"    contains=openvpnTodo

syntax keyword openvpnTodo contained TODO FIXME XXX NOT NOTE

"============================================================================
" Section:  Autogenerated Groups  {{{2
"============================================================================

" Begin autogenerated section.
" openvpn:     "OpenVPN 2.0.5 i386-redhat-linux-gnu [SSL] [LZO] [EPOLL] built on Nov  4 2005"
" openvpn2vim: "openvpn2vim,v 1.2 2005/12/04 17:09:33 ehaar Exp"

syntax region openvpnNoArgOption matchgroup=openvpnOption start=+^\s*\(
     \auth-nocache\|ccd-exclusive\|client\|client-cert-not-required\|
     \comp-lzo\|comp-noadapt\|disable\|disable-occ\|down-pre\|duplicate-cn\|
     \float\|genkey\|http-proxy-retry\|ifconfig-noexec\|ifconfig-nowarn\|
     \management-hold\|management-query-passwords\|mktun\|mlock\|mtu-test\|
     \no-iv\|no-replay\|nobind\|passtos\|persist-key\|persist-local-ip\|
     \persist-tun\|ping-timer-rem\|pull\|push-reset\|remote-random\|rmtun\|
     \show-ciphers\|show-digests\|show-engines\|show-tls\|single-session\|
     \suppress-timestamps\|test-crypto\|tls-client\|tls-exit\|tls-server\|
     \up-delay\|up-restart\|username-as-common-name
     \\)+ end=+$+ contains=openvpnSpuriousValue

syntax region openvpnArgOption matchgroup=openvpnOption start=+^\s*\(
     \askpass\|auth\|auth-retry\|auth-user-pass\|auth-user-pass-verify\|
     \ca\|cd\|cert\|chroot\|cipher\|client-config-dir\|client-connect\|
     \connect-freq\|connect-retry\|crl-verify\|daemon\|dev\|dev\|dev-node\|
     \dev-type\|dh\|down\|echo\|engine\|explicit-exit-notify\|fragment\|
     \group\|hand-window\|hash-size\|http-proxy\|http-proxy-option\|
     \ifconfig\|ifconfig-pool\|ifconfig-pool-persist\|ifconfig-push\|
     \inetd\|ipchange\|iroute\|keepalive\|key\|key-method\|keysize\|
     \link-mtu\|local\|log\|log-append\|lport\|management\|
     \max-clients\|max-routes-per-client\|mode\|mssfix\|mtu-disc\|mute\|nice\|
     \ping\|ping-exit\|ping-restart\|pkcs12\|plugin\|port\|proto\|push\|
     \redirect-gateway\|remap-usr1\|remote\|reneg-bytes\|reneg-pkts\|
     \replay-persist\|replay-window\|resolv-retry\|route\|route-delay\|
     \route-up\|rport\|secret\|secret\|server\|server-bridge\|setenv\|shaper\|
     \socks-proxy\|status\|status-version\|syslog\|tcp-queue-limit\|tls-auth\|
     \tls-remote\|tls-timeout\|tls-verify\|tmp-dir\|tran-window\|tun-mtu\|
     \txqueuelen\|up\|user\|verb\|writepid
     \\)+ end=+$+ contains=openvpnSpecial,openvpnQuote

" End autogenerated section.

"============================================================================
" Section:  Group Linking  {{{1
"============================================================================

OpenvpnHiLink openvpnOption        Keyword
OpenvpnHiLink openvpnComment       Comment
OpenvpnHiLink openvpnQuote         Constant
OpenvpnHiLink openvpnSpecial       Special
OpenvpnHiLink openvpnSpuriousValue Error
OpenvpnHiLink openvpnTodo          Todo   

"============================================================================
" Section:  Clean Up    {{{1
"============================================================================

delcommand OpenvpnHiLink

let b:current_syntax = "openvpn"

if main_syntax == 'openvpn'
  unlet main_syntax
endif

" Autoconfigure vim indentation settings
" vim:ts=4:sw=4:sts=4:fdm=marker
